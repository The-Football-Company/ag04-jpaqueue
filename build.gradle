plugins {
	id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

apply plugin: 'idea'

group = 'com.ag04.jpaqueue'
sourceCompatibility = '11'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

repositories {
    mavenCentral()
}


publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
    repositories {
        if (project.hasProperty('github_username') && project.hasProperty('github_password')) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ag04/ag04-jpaqueue")
                credentials {
                    username github_username
                    password github_password
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn publish

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
